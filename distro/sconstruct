import os

# Aliases
# scons           - builds everything
# scons -c        - cleans everything
# scons asgard    - builds just asgard executable
# scons db        - builds just asgard.db3
# scons manpage   - compresses manpage
# scons install   - install asgard to system

# Get name of OS
osName = os.uname()[0]

AddOption(
	'--prefix',
	dest='prefix',
	type='string',
	nargs=1,
	action='store',
	metavar='DIR',
	help='installation prefix'
)

# Build the environment and export it for child sconscripts
if osName == 'FreeBSD':
   env = Environment(PREFIX = GetOption('prefix'), CXX='clang++', LIBPATH='/usr/local/lib/', \
                     LD_LIBRARY_PATH='/usr/local/lib/', CPPFLAGS='-std=c++11')
else:
   env = Environment(PREFIX = GetOption('prefix'), CXX='clang++', CPPFLAGS='-g -std=c++11')

Export('env')

# Child conscripts
SConscript([
   'database/sconscript',
   'src/sconscript',
   'docs/sconscript'
])


Import("asgard")
Import("asgarddb3")
Import("manpage")

# Make our Aliases

# 'asgard' Alias
env.Alias("asgard" , asgard)

# 'db' Alias
env.Alias("db"     , asgarddb3)

# 'install' Alias
env.Alias("manpage", manpage)

# 'install' Alias
if osName == 'FreeBSD':
   env.Alias("install", env.Install("$PREFIX/usr/games", asgard))
   env.Alias("install", env.Install("$PREFIX/usr/local/share/asgard/", Dir("scripts")))
   env.Alias("install", env.Install("$PREFIX/usr/local/share/asgard/", Dir("spritesheet")))
   env.Alias("install", env.Install("$PREFIX/usr/local/share/asgard/database", asgarddb3))
   env.Alias("install", env.Install("$PREFIX/usr/local/man/man1", manpage))
else:
   env.Alias("install", env.Install("$PREFIX/usr/games", asgard))
   env.Alias("install", env.Install("$PREFIX/usr/share/asgard/", Dir("scripts")))
   env.Alias("install", env.Install("$PREFIX/usr/share/asgard/", Dir("spritesheet")))
   env.Alias("install", env.Install("$PREFIX/usr/share/asgard/database", asgarddb3))
   env.Alias("install", env.Install("$PREFIX/usr/share/man/man1", manpage))

